// *****************************************************************************
// Название: Загрузка договоров из Excel
// Описание: Загрузка договоров из Excel
// Кнопка вызова: 0
// Подпись кнопки: Загрузка договоров из Excel
// Язык: FuncScript
// Вызов по событию: 
// Без подтверждения: 0
// *****************************************************************************
//

ExecuteScript(ProgramPath() + 'ProcDocs\sql_insert.prd');

Var ('sFileName', string, '');

If(
   SELECTFILE ('sFileName', 'Укажите файл для загрузки', '(*.xlsx)|*.xlsx'),,
   RaiseException ('Выполнение отменено')
);

VAR ('select', Integer, 0);
VAR ('tmpPlaceID', String, '');
VAR ('tmpKind', String, '');
VAR ('tmpName', String, '');
VAR ('tmpNumber', String, '');
VAR ('tmpDate', String, '');

OPENQUERY ('PLC', 'SELECT PLACEID, LICENCENO, LICENCEDATE, LICENCETYPE, NAME  FROM STORES WHERE ISNULL(IN_ARCHIVE,0)=0','STS_DB');
select := SELECTVALUES ('Выберите разрешительный документ', 'PLC',
             [['LICENCENO', 'Номер лицензии', 50],
              ['NAME', 'Наименование СВХ', 50],
              ['LICENCEDATE', 'Дата лицензии', 20]],
             [['PLACEID', 'tmpPlaceId'],
              ['LICENCENO', 'tmpNumber'],
              ['NAME', 'tmpName'],
              ['LICENCEDATE', 'tmpDate'], ['LICENCETYPE', 'tmpKind']], 'PLACEID', 'STS_DB');

CLOSEDATASET('PLC');
IF (select = 0,RaiseException('Выполнение отменено'));

tmpKind := CASE (tmpKind, ['1', 'lic_Certificate',
                           '2', 'lic_Licence',
                           '3', 'lic_Permition',
                           '4', 'lic_PermZtk',
                           '5', 'lic_TempZtk'
                          ], 'lic_Certificate');


VAR ('XL', variant);
VAR ('iSheetsCount', integer);
VAR ('iRowsCount', integer);


XL:= CreateOleObject('Excel.Application');
XL.Workbooks.Add(sFileName);
XL.Visible := false;
iRowsCount := XL.ActiveSheet.UsedRange.Rows.Count;

VAR ('i', integer, 2);
IF (iRowsCount>0,,
   BLOCK(
         XL.ActiveWorkbook.Close;
         XL.Quit;
         RAISEEXCEPTION ('Нет данных для чтения');
   )
);


Var ('iLoadedContracts', integer, 0);


SHOWPROGRESS ('Загрузка товаров');
CREATELOGFILE ('log.txt');
APPENDLOGFILE('log.txt', 'Загрузка договоров их Excel: ' + sFileName);

while (i <= iRowsCount,
      BLOCK(
            //sCellNumPP:= XL.ActiveSheet.Cells(i,1);
            IF (length(XL.ActiveSheet.Cells(i,1)) > 0,
               BLOCK(
                    Insert2 ('JOURNAL_MASTER_ID', '(SELECT (ISNULL(MAX(JOURNAL_MASTER_ID),0) + 1) FROM CONTRACTINFO)', 3);
                    Insert2 ('JOURNAL_UUID', GenerateUUID());
                    Insert2 ('PLACEID', tmpPlaceId);
                    Insert2 ('WH_NAME', tmpName);
                    Insert2 ('WH_CERTKIND', tmpKind);
                    Insert2 ('WH_CERTNUMBER', tmpNumber);
                    Insert2 ('WH_CERTDATE', tmpDate, 2);

                    IF (LENGTH (XL.ActiveSheet.Cells(i,2))>8,
                       Insert2 ('DOGDATE', XL.ActiveSheet.Cells(i,2), 2)
                    );
                    IF (LENGTH (XL.ActiveSheet.Cells(i,3))>8,
                       Insert2 ('LIMITDATE', XL.ActiveSheet.Cells(i,3), 2)
                    );
                    Insert2 ('DOGNUMBER', XL.ActiveSheet.Cells(i,1));
                    Insert2 ('ORG_NAME', XL.ActiveSheet.Cells(i,4));
                    Insert2 ('ORG_ADDRESS', XL.ActiveSheet.Cells(i,5));
                    Insert2 ('ORG_COUNTRYCODE', XL.ActiveSheet.Cells(i,6));

                    TRYEXCEPT(
                       BLOCK(
                             Insert2 ('','',0,'CONTRACTINFO','dbJournals');
                             iLoadedContracts := iLoadedContracts + 1
                       ),,
                       BLOCK(
                             APPENDLOGFILE('log.txt', 'Ошибка загрузки договора: '+XmlNodeValue(XmlNodeFind(dContract, 'ACCOUNTNum')));
                             APPENDLOGFILE('log.txt', EXCEPTIONMESSAGE());
                       )
                    );
               )
            );


            SETPROGRESS((100/iRowsCount)*i);
            i := i + 1;
      )
);

HIDEPROGRESS();
XL.ActiveWorkbook.Close;
XL.Quit;

APPENDLOGFILE('log.txt', 'Загружено: '+iLoadedContracts + ' договоров ');
SHOWLOGFILE ('log.txt');
