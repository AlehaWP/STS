// *****************************************************************************
// Название: do1modif_reg_error
// Описание:
// Кнопка вызова: 0
// Подпись кнопки: do1modif_reg_error
// Язык: FuncScript
// Вызов по событию:
// Без подтверждения: 0
// *****************************************************************************
//

IF (VAREXISTS ('sSQL') = 0, VAR ('sSQL', String, ''));
VAR ('dtDoDt', DateTime, XMLNODEATTRIBUTE (xmlMainNode, 'dodt'));
VAR ('iPlaceId', Integer, XMLNODEATTRIBUTE (xmlMainNode, 'whid'));
// VAR ('sDocumentId', String, XMLNODEATTRIBUTE (xmlMainNode, 'do1id'));
VAR ('sInitEnvelopeId', String, XMLNODEATTRIBUTE (xmlMainNode, 'initEnvelopeId'));
VAR ('sStatus', String, '');
VAR ('sSubStatus', String, '');

IF (LOCATE ('KRD_DOP_2', 'PLACEID;DOCUMENTID', [iPlaceId, sInitEnvelopeId]),
  Block(
    IF (KRD_DOP_2.DOC_REG_STATUS <> '3',
      Block(
        sSQL := 'UPDATE' +
                ' KRD_DOP' +
                ' SET' +
                ' DOC_REG_STATUS=' +char(39)+ '1' +char(39)+
                ' WHERE' +
                ' PLACEID=' + KRD_DOP_2.PLACEID +
                ' AND ID=' + KRD_DOP_2.ID +
                ' AND COUNTER=' + KRD_DOP_2.COUNTER;
        EXECUTESQL ('STS_DB', sSQL);

        IF (LOCATE ('KRD_MAIN_2', 'PLACEID;ID', [KRD_DOP_2.PLACEID, KRD_DOP_2.ID]),
          Block(
            sStatus := 'Отказ в регистрации комм. акта №' + KRD_DOP_2.DOC_NO;
            sSubStatus := REGEXREPLACE (xmlMainNode.ErrorDsc, '(\n)', ' ', 1);
            sSQL := 'UPDATE' +
                    ' KRD_MAIN' +
                    ' SET' +
                    ' STATUS_EPS=' +char(39)+ sStatus +char(39)+
                    ' WHERE' +
                    ' PLACEID=' + KRD_MAIN_2.PLACEID +
                    ' AND MAIN_ID=' + KRD_MAIN_2.MAIN_ID;
            EXECUTESQL ('STS_DB', sSQL);

            // внешняя функция WriteEpsLog, подключена в скрипте eps.imp
            WriteEpsLog(
                KRD_MAIN_2.PLACEID,
                KRD_MAIN_2.ID,
                0,
                KRD_MAIN_2.DOCUMENTID,
                'ДО-1',
                KRD_MAIN_2.NBD,
                KRD_MAIN_2.BD_DATE,
                sStatus,
                Date()+Time(1),
                GENERATEUUID (),
                sSubStatus,
                sXmlFileName, // внешняя переменная из скрипта eps.imp
                0,
                dtDoDt,
                0
            ); // WriteEpsLog
          )
        ); // IF
      ),
      Block(
        WriteLog (
            'EPSIMP',
            '(' + sXmlFileName + '): Комм. акт ' + KRD_DOP_2.DOC_NO + ' ' + FDT ('DD.MM.YYYY', KRD_DOP_2.DOC_DATETIME) + ' зарегистрирован, невозможно загрузить отказ в регистрации.'
        );
      )
    ); // IF
  ),
  Block(
    WriteLog(
        'EPSIMP',
        '(' + sXmlFileName + '): Не найден комм. акт с параметрами PLACEID: ' + iPlaceId + '; DOCUMENTID: ' + sDocumentId
    )
  )
); // IF
